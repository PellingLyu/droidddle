apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions.encoding = "UTF-8"

    defaultConfig {
        applicationId "org.goodev.droidddle"
        minSdkVersion 15
        targetSdkVersion 23
        versionName rootProject.ext.versionName
        versionCode rootProject.ext.versionCode
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    signingConfigs {
        release {
            def Properties localProps = new Properties()
            localProps.load(new FileInputStream(file('../local.properties')))
            def Properties keyProps = new Properties()
            if (localProps['keystore.props.file'] != null) {
                keyProps.load(new FileInputStream(file(localProps['keystore.props.file'])))
            }
            storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
            keyAlias keyProps["alias"] ?: ""
            storePassword keyProps["storePass"] ?: ""
            keyPassword keyProps["pass"] ?: ""
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    productFlavors {
        play {
            buildConfigField "boolean", "IS_PLAY", "true"
            buildConfigField "String", "AppId", '"0805b9c50cf07"'
            buildConfigField "String", "AppSecret", '"2735d39422ee5"'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    dexOptions {
//        incremental true
        javaMaxHeapSize "2g"
    }
}

retrolambda {
    jvmArgs '-noverify'
}
dependencies {
    retrolambdaConfig 'net.orfjackal.retrolambda:retrolambda:1.8.0'
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:palette-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:customtabs:23.1.0'

    compile project(':preferencefragment')
    compile project(':crop_image')
    compile 'com.jakewharton:butterknife:5.1.1'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.1.3'
    compile "com.facebook.fresco:fresco:${FRESCO_VERSION}"
    compile "com.facebook.fresco:imagepipeline-okhttp:${FRESCO_VERSION}"
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile 'org.jsoup:jsoup:1.8.1'
    compile 'com.google.android.apps.muzei:muzei-api:2.0'
    compile project(':ChangeLogLibrary')
    compile project(':seekbar-compat')
    compile project(':colorpicker')
    compile 'com.nononsenseapps:filepicker:2.4.2'
    playCompile fileTree(dir: 'libs/play', include: '*.jar')
    playCompile 'com.google.android.gms:play-services-ads:8.3.0'

    compile 'com.github.tajchert:nammu:1.1.1'
}
